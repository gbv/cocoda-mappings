# Extract DDC mappings from GND as JSKOS mappings

# Usage: catmandu convert marcxml --skip-errors 1 --fix gnd-ddc.fix to Null < gnd.xml > gnd-ddc.ndjson

# Only keep those that have a DDC notation
select marc_match(083$a, '.+')

# GND URI
marc_map(024$0, gnd)

# Make a valid JSKOS mapping
move_field(gnd, "from.memberSet.0.uri")
# Add GND prefLabel
marc_map(150$a, "from.memberSet.0.prefLabel.de")

add_field("fromScheme.uri", "http://bartoc.org/en/node/430")
add_field("toScheme.uri", "http://bartoc.org/en/node/241")
# add_field("type.0", "http://www.w3.org/2004/02/skos/core#exactMatch")

# TODO
add_field("creator.0.prefLabel.de", "DNB")
# add_field("creator.0.uri", "http://viaf.org/viaf/156508705")

# DDC URIs
marc_map(083$a, ddcs.$append)

rm(record)
rm(_id)

# Iterate over DDCs and export one mapping for each
do list(path: ddcs, var: ddc)
  rm(ddcs)
  prepend(ddc, "http://dewey.info/class/")
  append(ddc, "/e23/")
  move_field(ddc, "to.memberSet.0.uri")
  add_to_exporter(., JSON, line_delimited: 1, canonical: 1)
end
